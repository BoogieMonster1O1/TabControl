plugins {
    id 'maven-publish'
    id 'fabric-loom' version "0.4-SNAPSHOT"
    id "com.github.johnrengelman.shadow" version "6.0.0"
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

repositories {
    mavenCentral()
    jcenter()
    maven {
        name = 'Fabric'
        url = 'http://maven.fabricmc.net/'
    }
    maven {
        name = 'SpongePowered'
        url = 'http://repo.spongepowered.org/maven'
    }
    maven {
        name = 'mojang'
        url = 'https://libraries.minecraft.net/'
    }
    maven {
        name = 'legacy-fabric'
        url = 'https://dl.bintray.com/legacy-fabric/Legacy-Fabric-Maven'
    }
    maven {
        name = "SwiftPublicRepo"
        url = uri("https://raw.githubusercontent.com/SwiftHQ/SwiftPublicRepo/master")
    }
}

minecraft {
    intermediaryUrl = {
        return "https://dl.bintray.com/legacy-fabric/Legacy-Fabric-Maven/net/fabricmc/intermediary/" + it + "/intermediary-" + it + "-v2.jar";
    }
}

tasks.shadowJar.dependsOn tasks.remapJar

dependencies {
    implementation "com.google.guava:guava:23.5-jre"
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation ("net.fabricmc:fabric-loader-1.8.9:${project.loader_version}") {
        exclude module: "guava"
    }

    modImplementation("net.swifthq:SwiftAPI:1.7.1")
    modImplementation "net.fabricmc.fabric-api:fabric-api:0.7.1"
    implementation "com.mojang:datafixerupper:4.0.26"
    shadow "com.mojang:datafixerupper:4.0.26"
}

processResources {
    inputs.property "version", project.version

    from(sourceSets.main.resources.srcDirs) {
        include "fabric.mod.json"
        expand "version": project.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude "fabric.mod.json"
    }
}

shadowJar {
    relocate 'com.mojang.serialization', 'io.github.hydos.tabcontrol.shadowed.datafixerupper.com.mojang.serialization'
    relocate 'com.mojang.datafixers', 'io.github.hydos.tabcontrol.shadowed.datafixerupper.com.mojang.datafixers'
}

// ensure that the encoding is set to UTF-8, no matter what the system default is
// this fixes some edge cases with special characters not displaying correctly
// see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
// if it is present.
// If you remove this task, sources will not be generated.
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = "sources"
    from sourceSets.main.allSource
}

jar {
    from "LICENSE"
}

// configure the maven publication
publishing {
    publications {
        mavenJava(MavenPublication) {
            // add all the jars that should be included when publishing to maven
            artifact(remapJar) {
                builtBy remapJar
            }
            artifact(sourcesJar) {
                builtBy remapSourcesJar
            }
        }
    }

    // select the repositories you want to publish to
    repositories {
        // uncomment to publish to the local maven
        // mavenLocal()
    }
}
